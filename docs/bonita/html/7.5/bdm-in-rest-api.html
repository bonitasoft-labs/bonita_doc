<div style="position: relative;"><a href="https://github.com/bonitasoft/bonita-doc/edit/7.7/md/bdm-in-rest-api.md"><img style="position: absolute; top: -1px; right: -40px; border: 0; z-index=-100;" src="https://camo.githubusercontent.com/365986a132ccd6a44c23a9169022c0b5c890c387/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f7265645f6161303030302e706e67" alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_red_aa0000.png"></a></div>
<h1>How to manage BDM in REST API extensions</h1>
<p>This page demonstrates best practices when developing a <a href="?page=rest-api-extensions" ng-click="contentCtrl.goTo($event, 'rest-api-extensions')">REST API Extension</a> accessing <a href="?page=define-and-deploy-the-bdm" ng-click="contentCtrl.goTo($event, 'define-and-deploy-the-bdm')">Business Data</a>.<br>
Special attention is paid on performance matters.</p>
<h2>First use case</h2>
<p>Image below shows the BDM model used for the first use-case:</p>
<p><img src="bonita/images/7.7/bdm_model_for_rest_api_01.png" alt="BDM model used" title="BDM model used"></p>
<p>Note that the relationships from car to wheel1, wheel2, wheel3, wheel4 are <strong><a href="?hash=lazy_eager_loading&amp;page=define-and-deploy-the-bdm" ng-click="contentCtrl.goTo($event, 'define-and-deploy-the-bdm', 'lazy_eager_loading')">lazy</a> loaded</strong> (in the Studio, it is
configured by selecting the <code>Only load related objects when needed</code> radio button).</p>
<h2>Groovy code sample</h2>
<p>Below is an example of a REST API extension groovy script that accesses the Business Data model.</p>
<div class="alert alert-info" role="alert">
<p><strong>Good practices</strong> are:</p>
<ul>
<li>If lazy loaded objects are not necessary in the response, ONLY extract the information needed. This avoids costly loading of unnecessary objects.</li>
<li>If lazy loaded objects should always be returned in the response, consider changing the relation from <a href="?hash=lazy_eager_loading&amp;page=define-and-deploy-the-bdm" ng-click="contentCtrl.goTo($event, 'define-and-deploy-the-bdm', 'lazy_eager_loading')" class="alert-link">'lazy' to 'eager'</a>
in the model.</li>
</ul>
</div>
<pre><code class="language-groovy">
class CarManagement implements RestApiController {

    private static final Logger LOGGER = LoggerFactory.getLogger(CarManagement.class)
    
    @Override
    RestApiResponse doHandle(HttpServletRequest request, RestApiResponseBuilder responseBuilder, RestAPIContext context) {
        // To retrieve query parameters use the request.getParameter(..) method.
        // Be careful, parameter values are always returned as String values
        
        // Retrieve p parameter
        def p = request.getParameter &quot;p&quot;
        if (p == null) {
            return buildResponse(responseBuilder, HttpServletResponse.SC_BAD_REQUEST,&quot;&quot;&quot;{&quot;error&quot; : &quot;the parameter p is missing&quot;}&quot;&quot;&quot;)
        }
        
        // Retrieve c parameter
        def c = request.getParameter &quot;c&quot;
        if (c == null) {
            return buildResponse(responseBuilder, HttpServletResponse.SC_BAD_REQUEST,&quot;&quot;&quot;{&quot;error&quot; : &quot;the parameter c is missing&quot;}&quot;&quot;&quot;)
        }
        
        // use APIClient to retrieve the CarDAO class:
        def carDAO = context.apiClient.getDAO(CarDAO.class)
        
        def currentModel = &quot;DeLorean&quot;
        // Fetch the cars that match the search criteria:
        List&lt;Car&gt; cars = carDAO.findByModel(currentModel, p as int, c as int)
        
        // Prepare the Json result:
        // Do NOT return directly the list &quot;cars&quot;, as the entire list of Wheel objects would be fetched by lazy loading when calling the JsonBuilder toString method.
        // Instead, ONLY select the fields that are necessary for your business logic (alternative methods are also available, see below in this page):
        def carModels = [:]
        for (Car car : cars) {
            // return the fields &quot;id&quot;, &quot;buildYear&quot;, &quot;color&quot;:
            carModels.put(&quot;id: ${car.persistenceId}&quot;, &quot;${car.buildYear} - ${car.color}&quot;)
        }
        
        def result = [ &quot;model&quot; : currentModel, &quot;number of cars&quot; : cars.size(), &quot;cars&quot; : carModels ]
        
        // Attention: do not use &quot;toPrettyString()&quot; if your response is large, as the page &quot;size&quot; may be uselessly big:
        return buildResponse(responseBuilder, HttpServletResponse.SC_OK, new JsonBuilder(result).toString())
    }
}
</code></pre>
<h2>Rest API Response content</h2>
<p>Below is an example of the resulting response (the json is formatted to improve readability):</p>
<pre><code class="language-json">{
&quot;model&quot;: &quot;DeLorean&quot;,
&quot;number of cars&quot;: 30,
    &quot;cars&quot;: {
        &quot;id: 1&quot;: &quot;1933 - Red&quot;,
        &quot;id: 6&quot;: &quot;1938 - Rainbow&quot;,
        &quot;id: 20&quot;: &quot;2002 - Green&quot;,
        &quot;id: 24&quot;: &quot;1940 - Yellow&quot;,
        &quot;id: 33&quot;: &quot;1954 - Purple&quot;,
        &quot;id: 46&quot;: &quot;1932 - Rainbow&quot;,
        &quot;id: 50&quot;: &quot;2008 - Teal&quot;,
        &quot;id: 51&quot;: &quot;1979 - Purple&quot;,
        &quot;id: 53&quot;: &quot;1942 - Purple&quot;,
        &quot;id: 60&quot;: &quot;1941 - Rainbow&quot;,
        &quot;id: 71&quot;: &quot;1987 - Orange&quot;,
        &quot;id: 75&quot;: &quot;1956 - Green&quot;,
        &quot;id: 82&quot;: &quot;1938 - Rainbow&quot;,
        &quot;id: 92&quot;: &quot;1955 - Rainbow&quot;,
        &quot;id: 100&quot;: &quot;1965 - Orange&quot;,
        &quot;id: 119&quot;: &quot;1992 - Blue&quot;,
        &quot;id: 128&quot;: &quot;2015 - Purple&quot;,
        &quot;id: 130&quot;: &quot;1932 - Yellow&quot;,
        &quot;id: 142&quot;: &quot;1951 - Grey&quot;,
        &quot;id: 143&quot;: &quot;1930 - White&quot;,
        &quot;id: 146&quot;: &quot;1937 - Teal&quot;,
        &quot;id: 147&quot;: &quot;1961 - Blue&quot;,
        &quot;id: 152&quot;: &quot;1939 - Purple&quot;,
        &quot;id: 158&quot;: &quot;1977 - Grey&quot;,
        &quot;id: 159&quot;: &quot;2013 - Rainbow&quot;,
        &quot;id: 160&quot;: &quot;1997 - Purple&quot;,
        &quot;id: 163&quot;: &quot;1973 - Rainbow&quot;,
        &quot;id: 164&quot;: &quot;1940 - Purple&quot;,
        &quot;id: 165&quot;: &quot;1933 - Teal&quot;,
        &quot;id: 172&quot;: &quot;1983 - Rainbow&quot;
    }
}
</code></pre>
<div class="alert alert-info" role="alert">
<p>Note that Wheels are not returned, only necessary information is fetched.<br>
As a result, performance is efficient</p>
</div>
<h2>Troubleshooting bad practices</h2>
<div class="alert alert-warning" role="alert">
<p><strong><i class="fa fa-exclamation-triangle"></i> Practices leading to poor performance</strong></p>
<p>Since wheel1, wheel2, wheel3, wheel4 are lazy loaded, they are <strong>not retrieved</strong> directly when retrieving a Car.
The retrieval of related Wheel objects is only performed <strong>when accessing the fields</strong> (via getWheel1(), ...), if necessary.</p>
<p>However, when building the response, the <code>JsonBuilder's toString</code> method  <strong>implicitly fetches</strong> all lazy loaded fields (it calls all the field getters).<br>
So, if a large number of Business Data is returned and if you have lazy loaded fields in the returned objects, numerous queries are executed, leading to poor performance.</p>
<p>For example, if you don't follow the code sample above and write something like:</p>
<pre><code class="language-groovy">        def currentModel = &quot;DeLorean&quot;
        // Fetch the cars that match the search criteria:
        List&lt;Car&gt; cars = carDAO.findByModel(currentModel, p as int, c as int)
        def result = [ &quot;cars&quot; : cars ]
        return buildResponse(responseBuilder, HttpServletResponse.SC_OK, new JsonBuilder(result).toString())
</code></pre>
<p>The returned result will contain, for each car, the fields persistenceId, buildYear and color, allowing you to use these in your application(s).<br>
However, assuming you want to retrieve 10 cars of the &quot;Delorean&quot; model, this code will execute a total of <strong>41</strong> &quot;Select&quot; database queries</p>
<ul>
<li>1 query to get the cars,</li>
<li>then 4 queries per car to fetch each one of the <em>wheel</em> fields to build the JSON response (so 40 queries).</li>
</ul>
<p>In comparison, the code following good practises only performs <strong>a single Select database query</strong>.</p>
</div>
<h2>Other use cases</h2>
<p>The rest api extension example previously described in this page advices to:</p>
<ul>
<li>create a custom data structure for the response</li>
<li>copy only selected fields from the BDM object into this custom data structure</li>
</ul>
<p>In some cases, you may want to return the entire BDM object structure in the response:</p>
<ul>
<li>because it eases parsing the REST API Json result to build an Object</li>
<li>for maintenance reasons, when adding a new field to a BDM object, you may avoid to have to modify the Rest API extension code to include this new field</li>
</ul>
<h3>Returning the whole object without its lazy loaded fields</h3>
<p>The troobleshooting section gives an example using the Groovy <code>JsonBuilder</code> class leading to poor performance: it calls the getter of lazy loaded fields which
then fetches the data.
So using an alternate json builder implementation can solve this issue.</p>
<p>As the BDM object lazy loaded fields are marked with the Jackson's <code>@JsonIgnore</code> annotation and as the Jackson's library is available for use in the Rest API Extension,
the best candidate for this is to use the Jackson serializer to generate the json response.</p>
<pre><code class="language-groovy">
import com.fasterxml.jackson.databind.ObjectMapper
import com.fasterxml.jackson.databind.SerializationFeature
    
    
class CarManagement implements RestApiController {
    
    private static final Logger LOGGER = LoggerFactory.getLogger(CarManagement.class)
    
    // Use a shared instance for performance reason (see https://github.com/FasterXML/jackson-docs/wiki/Presentation:-Jackson-Performance)
    private static final ObjectMapper jsonBuilder = new ObjectMapper()
    static {
        // needed to serialize BDM object because of the Bonita lazy loading mechanism
        jsonBuilder.disable(SerializationFeature.FAIL_ON_EMPTY_BEANS)
    }
    
    @Override
    RestApiResponse doHandle(HttpServletRequest request, RestApiResponseBuilder responseBuilder, RestAPIContext context) {
        // To retrieve query parameters use the request.getParameter(..) method.
        // Be careful, parameter values are always returned as String values
        
        // Retrieve p parameter
        def p = request.getParameter &quot;p&quot;
        if (p == null) {
            return buildResponse(responseBuilder, HttpServletResponse.SC_BAD_REQUEST,&quot;&quot;&quot;{&quot;error&quot; : &quot;the parameter p is missing&quot;}&quot;&quot;&quot;)
        }
        
        // Retrieve c parameter
        def c = request.getParameter &quot;c&quot;
        if (c == null) {
            return buildResponse(responseBuilder, HttpServletResponse.SC_BAD_REQUEST,&quot;&quot;&quot;{&quot;error&quot; : &quot;the parameter c is missing&quot;}&quot;&quot;&quot;)
        }
        
        // use APIClient to retrieve the CarDAO class:
        def carDAO = context.apiClient.getDAO(CarDAO.class)
        
        def currentModel = &quot;DeLorean&quot;
        // Fetch the cars that match the search criteria:
        List&lt;Car&gt; cars = carDAO.findByModel(currentModel, p as int, c as int)
        
        // Prepare the Json result:
        def result = [ &quot;model&quot; : currentModel, &quot;number of cars&quot; : cars.size(), &quot;cars&quot; : cars ]
        
        return buildResponse(responseBuilder, HttpServletResponse.SC_OK, jsonBuilder.writeValueAsString(result))
    }
</code></pre>
<h3>Returning the whole object with an API link load in the lazy fields</h3>
<p>The idea is to create a custom Json serializer.<br>
A custom Json serializer is a class which extends <em>com.fasterxml.jackson.databind.JsonSerializer</em>. There is a method <em>serialize</em> to implement, which has the responsability to serialize the input model into Json.<br>
The custom Json serializer has to come with an other class, an object mapper,  which extends <em>com.fasterxml.jackson.databind.ObjectMapper</em>.<br>
This mapper registers a simple module (<em>com.fasterxml.jackson.databind.module.SimpleModule</em>), which has to contain the custom serializer.<br>
At the end, in your rest API endpoint, you interact with the mapper.</p>
<p>Here is an implementation example for the object Car which has four lazy attributes of type Wheel:</p>
<p>The serializer takes a Car in input, and build a Json object for it. The wheels are replaced with links to an other Rest API extension with the car ID and the wheel number in parameter. Calling this API will return the wheel. This is a classic lazy behavior.</p>
<pre><code class="language-groovy">/***********************
 ***** SERIALIZER ******
 ***********************/

import com.fasterxml.jackson.core.JsonGenerator
import com.fasterxml.jackson.databind.JsonSerializer
import com.fasterxml.jackson.databind.SerializerProvider

class CarSerializer extends JsonSerializer&lt;Car&gt;{

	@Override
	public void serialize(Car car, JsonGenerator jgen, SerializerProvider provider)throws IOException, JsonProcessingException {
		jgen.writeStartObject()
		
		jgen.writeNumberField(&quot;carID&quot;, car.getPersistenceId())
		jgen.writeStringField(&quot;model&quot;, car.getModel())
		jgen.writeNumberField(&quot;buildYear&quot;, car.getBuildYear())
		jgen.writeStringField(&quot;color&quot;, car.getColor())
		jgen.writeStringField(&quot;wheel1Request&quot;, getWheelRequest(car.getPersistenceId(), 1))
		jgen.writeStringField(&quot;wheel2Request&quot;, getWheelRequest(car.getPersistenceId(), 2))
		jgen.writeStringField(&quot;wheel3Request&quot;, getWheelRequest(car.getPersistenceId(), 3))
		jgen.writeStringField(&quot;wheel4Request&quot;, getWheelRequest(car.getPersistenceId(), 4))
		
		jgen.writeEndObject()
	}
	
	private String getWheelRequest(Long carID, Integer wheelNum) {
		return String.format('../API/extension/wheel?p=0&amp;c=10&amp;carID=%s&amp;wheelNum=%s', carID, wheelNum)
	}

}

/***********************
 ******* MAPPER ********
 ***********************/

import com.fasterxml.jackson.databind.ObjectMapper
import com.fasterxml.jackson.databind.SerializationFeature
import com.fasterxml.jackson.databind.module.SimpleModule

class CarObjectMapper extends ObjectMapper {
	public CarObjectMapper () {
	    SimpleModule module = new SimpleModule()
	    module.addSerializer(Car.class, new CarSerializer())
	    registerModule(module)
    } 
}

/***********************
 ******** INDEX ********
 ***********************/

class CarIndex implements RestApiController {

    private static final Logger LOGGER = LoggerFactory.getLogger(CarIndex.class)
    private static final CarObjectMapper CAR_MAPPER = new CarObjectMapper()

    @Override
    RestApiResponse doHandle(HttpServletRequest request, RestApiResponseBuilder responseBuilder, RestAPIContext context) {
        def p = request.getParameter &quot;p&quot;
        def c = request.getParameter &quot;c&quot;
		
		def carDAO = context.apiClient.getDAO(CarDAO.class)
		int startIndex = (p as Integer)*(c as Integer)
		int endIndex = c as Integer
		List&lt;Car&gt; cars = carDAO.find(startIndex, endIndex)
		
		def result = CAR_MAPPER.writeValueAsString(cars)
		
        return buildResponse(responseBuilder, HttpServletResponse.SC_OK, result)
    }

    RestApiResponse buildResponse(RestApiResponseBuilder responseBuilder, int httpStatus, Serializable body) {
        return responseBuilder.with {
            withResponseStatus(httpStatus)
            withResponse(body)
            build()
        }
    }

}
</code></pre>
<h2>Known limitations</h2>
<h3>Returning the object with SOME of its lazy loaded fields ONLY</h3>
<p>This use case is not supported. In other words it is necessary to use one database request per lazy loaded field you wish to retrieve.</p>
<!-- Generated on Wed Jan 24 2018 13:27:25 GMT+0100 (Paris, Madrid) -->